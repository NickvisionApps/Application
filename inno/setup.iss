; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName           "Nickvision Application"
#define MyAppShortName      "Application"
#define MyAppVersion        "2025.9.0"
#define MyAppPublisher      "Nickvision"
#define MyAppURL            "https://nickvision.org"
#define MyAppExeName        "org.nickvision.application.winui.exe"
#define StartYearCopyright  "2021"
#define CurrentYear         GetDateTimeString('yyyy','','')

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C43542D3-74E2-4499-9484-89848ACF4B2F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}

AppCopyright={#StartYearCopyright}-{#CurrentYear}  {#MyAppPublisher}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}

VersionInfoDescription={#MyAppName} installer
VersionInfoProductName={#MyAppName}
VersionInfoVersion={#MyAppVersion}

UninstallDisplayName={#MyAppName} {#MyAppVersion}
UninstallDisplayIcon={app}\Release\{#MyAppExeName}
AppPublisher={#MyAppPublisher}

ShowLanguageDialog=yes
UsePreviousLanguage=no
LanguageDetectionMethod=uilanguage

ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible

WizardStyle=modern
WizardResizable=yes
WizardSizePercent=120,130

DefaultDirName={autopf}\{#MyAppName}
LicenseFile=..\License.rtf
OutputDir=..\inno
OutputBaseFilename=NickvisionApplicationSetup
SetupIconFile=..\resources\org.nickvision.application.ico
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin
CloseApplications=yes
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[InstallDelete]
Type: filesandordirs; Name: "{app}\*"

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "vc_redist.exe"; DestDir: "{app}"; Flags: deleteafterinstall
Source: "windowsappruntimeinstall.exe"; DestDir: "{app}"; Flags: deleteafterinstall
Source: "..\build\org.nickvision.application.winui\Release\{#MyAppExeName}"; DestDir: "{app}\Release"; Flags: ignoreversion
Source: "..\build\org.nickvision.application.winui\Release\*"; DestDir: "{app}\Release"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{autoprograms}\{#MyAppShortName}"; Filename: "{app}\Release\{#MyAppExeName}"; Tasks: quicklaunchicon
Name: "{commondesktop}\{#MyAppShortName}"; Filename: "{app}\Release\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\vc_redist.exe"; Parameters: "/install /quiet /norestart"
Filename: "{app}\windowsappruntimeinstall.exe"; Parameters: "--quiet --force"
Filename: "{app}\Release\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent unchecked
